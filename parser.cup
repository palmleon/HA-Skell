import java_cup.runtime.*;
import java.util.HashMap;
import java.util.LinkedList;
import java.util.Stack;
import java.util.Iterator;

parser code {:

	private String outputFileName;

	public boolean noCompileErrors = true;
	
	/*
	 *  Define the name of the output file, containing the final Intermediate Representation in LLVM
	 *	Params: String outputFileName - name of the output file
	 *	Return: nothing
	 */
	public void setOutputFileName(String outputFileName) {
		this.outputFileName = outputFileName;
	}
	
	/* 
	 * 	Default Cup Function for reporting Syntax Errors
	 *  Params: java_cup.runtime.Symbol current - The last parsed symbol before raising the last Syntax Error 
	 *  Return: nothing
	 */
	public void syntax_error(java_cup.runtime.Symbol current) {
		report_error(current);
	}
	
	/* 
	 *  Inform the user that a Syntax Error has been raised, and inform them about the nature of the Syntax Error
	 *	Params: Object info - The last parsed symbol before raising the last Syntax Error
	 *  		String msg - The additional message describing the kind of Syntax Error
	 *	Return: nothing
	 */
    public void report_error(Object info) {
        System.err.print("ERROR: Syntax error");
		noCompileErrors = false;
        if (info instanceof Symbol)
            if (((Symbol)info).left != -1){
                int line = (((Symbol)info).left);
                int column = (((Symbol)info).right);
                System.err.print(" (line "+line+", column "+column+"): ");
            } else System.err.print(": ");
        else System.err.print(": ");
    }
	
:};

action code {:

	/*
	 * Boolean flag that provides additional information for debugging purposes, when raised
	 */
	public static boolean debug_mode = true; 
	
	/* 
	 * Class for the Type Tree
	 * The following Class defines the TypeTree structure
	 * Fields: String typeName - unique name for the Type
	 * 		   TypeTree[] - list containing all the subtrees for an instance of a compound type 
	 *				(useful for functions, lists, tuples and any additional compound type) 
	 *		   Hashmap<String, TypeTree> typeMap - a map containg, for any typeName, the corresponding standard TypeTree
	 *				(initialized at Parser initialization time)
	 */
	public static class TypeTree {
		
		private String typeName;
		private TypeTree[] childs; //better than an ArrayList because its size it's fixed
		private int nextChild = 0; // defines the next free slot where to allocate a child
		
		/* Basic Copy Constructor */
		TypeTree(String typeName, TypeTree[] childs) {
			this.typeName = typeName;
			this.childs = childs; // most types should have up to 2 type params
		}
		
		/* (Recursive) Copy Constructor */
		TypeTree(TypeTree tree) {			
			int length = tree.getChilds().length;
			this.typeName = tree.getTypeName();
			this.childs = new TypeTree[length];
			for (int i = 0; i < length; i++) {
				this.childs[i] = new TypeTree(tree.getChilds()[i]);
			}
		}
		
		/* Setter method for typeName */
		public void setTypeName(String typeName) {
			this.typeName = typeName;
		}
		
		
		/* Setter method for childs */
		public void setChilds(TypeTree[] childs){
			this.childs = childs;
		}
		
		/* Setter method for the single child */
		public void setChild(int pos, TypeTree child) {
			this.childs[pos] = child;
		}
		
		/* Method that adds a child at the next free slot (no position required)
		 * Params: TypeTree tree - the tree to be added
		 * Returns: nothing
		 */
		public void addChild(TypeTree tree) {
			this.setChild(nextChild, tree);
			nextChild++;
		}
		
		/* Getter method for typeName */
		public String getTypeName(){
			return this.typeName;
		}
		
		/* Getter method for childs */
		public TypeTree[] getChilds(){
			return this.childs;
		}
		
		/* Getter method for the single child */
		public TypeTree getChild(int pos) {
			return this.childs[pos];
		}
		
		/* 
		 * Method for Type Widening
		 * This Method is asymmetric: it takes the first type and tries to upcast it into the second one
		 * Params: String typeName1, typeName2 - the types to be compared
		 * Returns: The resulting widened type (null if widening is not possible) //TODO
		 */
		public static String widen(String typeName1, String typeName2) {
			String res = null;
			if (typeName1.equals(typeName2)) res = typeName1;
			else if (typeName1.equals("Int") && typeName2.equals("Double")) res = "Double";
			return res;
		}
		
		/* Method that checks Subtyping
		 * In Haskell, Type Hierarchy is not defined; instead, Hierarchy is represented by Typeclasses
		 * and their relationship.
		 * In this version of the Language, Typeclasses are not supported, so it is defined a simple
		 * Type Hierarchy where:
		 * - Any represents Type Variables (i.e. Java Generics)
		 * - Int is a subtype of Double
		 * The widen Method checks Subtyping for the single TreeNode
		 * Params: TypeTree other - the tree to be compared with the calling one
		 * Returns: Boolean - true if other is Parent of this, false otherwise
		 */
		public boolean isSubtype(TypeTree other) {
			boolean isSubtype = true;
			if (other.getTypeName().equals("Any")) return true;
			if (TypeTree.widen(this.getTypeName(), other.getTypeName()) == null) return false;
			if (this.getChilds().length != other.getChilds().length) return false;
			for (int i = 0; i < this.getChilds().length; i++) {
				isSubtype = isSubtype && this.getChild(i).isSubtype(other.getChild(i));
			}
			return isSubtype;
		}
		
		/* 
		 * Method for Type Equivalence Checking
		 * The isSubtype relationship is a Partial Ordering Relation, 
		 * Type Equivalence is equivalent to checking that 
		 * the first type is a subtype of the second one and viceversa.
		 * Params: TypeTree other - the Tree to compare with the calling one
		 * Returns: Boolean - true if the Trees are equivalent, false otherwise
		 */
		 
		public boolean isEquivalent(TypeTree other) {	
			return this.isSubtype(other) && other.isSubtype(this);
		}
		
		/*
		 * Static TypeMap that contains all the declared Types (allows to define custom Data Types), which is initialized during Parser initialization
		 */
		private static HashMap<String, TypeTree> typeMap = new HashMap<>();
		
		/* Insert a new Type inside the typeMap (if already present, the new one is discarded and a Semantic Error should arise)
		 * Params: String typeName - the name of the new Type
		 * 		   TypeTree tree - the corresponding type 
		 * Return: boolean - true if no error occured, false otherwise
		 */
		public static boolean addType(String typeName, TypeTree tree) {
			if (typeMap.containsKey(typeName)) return false;
			typeMap.put(typeName, tree);
			return true;
		}
		
		/* 
		 * Retrieve the TypeTree of a specific type from the typeMap, if present
		 * Params: String typeName - the name of the Type
		 * Return: the corresponding typeTree if the Type exists, null otherwise
		 */
		public static TypeTree getTypeTree(String typeName) {
			if (typeMap.containsKey(typeName))
				return typeMap.get(typeName);
			else return null;
		}
		
		/*
		 * Debugging method for dumping the whole TypeMap TODO
		 * Params: nothing
		 * Returns: nothing
		 */
		public static void dumpTypeMap(){
			System.out.println("DEBUG: PRINTING TYPEMAP!");
			typeMap.forEach
				( (k, v) -> {
					System.out.println("TopTypeName: " + k);
					v.dumpTypeTree(0);
				} );
		}
		
		/*
		 * Debugging method for dumping a single TypeTree TODO
		 * Params: int level - defines the level inside the TypeTree, used for hierarchical printing of the Tree
		 * Returns: nothing
		 */
		private void dumpTypeTree(int level){
			for (int i = 0; i < level; i++)
				System.out.print("\t");
			System.out.println("-> " + this.getTypeName());
			for (int i = 0; i < this.getChilds().length; i++) {
				this.getChild(i).dumpTypeTree(level+1);
			}
		}
		
	}
	
	/* Class for the Symbol Table Stack
	 * The following Class contains a Static instance of the SymTable Stack, together with methods
	 * that allow to push/pop SymTables and to extract an Entry from them
	 * Fields: symTableStack - the Static instance of the SymTable Stack
	 */
	
	public static class SymTableStack {
		
		/* Class for Symbol Table Entry
		 * All Entries inside any SymTable are instances of this Class
		 * Fields: TypeTree tree - type of the value/function
		 *		   boolean isAssigned - flag that checks whether the value/function has been assigned or not
		 */
		private static class SymTableEntry {
			
			private TypeTree tree;
			private boolean isAssigned = false;
			
			/* TODO UNCOMMENT WHEN GENERATING IR
			// corresponding name in the intermediate representation
			String llvm;
			*/
			
			SymTableEntry(TypeTree tree) {
				this.tree = tree;
				this.isAssigned = false;
			}
			
			public TypeTree getTypeTree() {
				return this.tree;
			}
			
			public boolean getIsAssigned() {
				return this.isAssigned;
			}
			
			public void setTypeTree(TypeTree tree) {
				this.tree = tree;
			}
			
			public void setIsAssigned(boolean isAssigned) {
				this.isAssigned = isAssigned;
			}
		}
		
		// Symbol Table Stack (there is only one global instance whose lifetime corresponds to the lifetime of the parser itself)
		private static LinkedList<HashMap<String, SymTableEntry>> symTableStack = new LinkedList<>();
		
		/* 
		 * Method that pushes a new SymTable to the Stack
		 * Params: nothing
		 * Return: nothing
		 */
		public static void pushSymTable() {
			symTableStack.push(new HashMap<String, SymTableEntry>());
		}
		
		/* 
		 * Method that pops the SymTable on top of the Stack
		 * Params: nothing
		 * Return: nothing
		 */
		public static void popSymTable() {
			symTableStack.pop();
		}
		
		/*
		 * Insert a new Entry in the SymTable on top of the Stack
		 * This method is sensitive to the context (it depends on the current position in the Parser Tree)
		 * The Parser must guarantee the correct SymTable for a given Entry
		 * Params: String id - the name of the Entry (the corresponding token)
		 * 		   TypeTree tree - the TypeTree of the Entry (built during reduction of Type Declarations)
		 * Return: nothing
		 */
		public static void putEntry(String id, TypeTree tree) {
			symTableStack.peek().put(id, new SymTableEntry(tree));
		}
		
		/*
		 * Check whether a certain Entry is present in the whole SymTable Stack
		 * Search is applied from the top of the Stack to the bottom;
		 * the first match is considered
		 * Params: String id - the name of the Entry (the corresponding token)
		 * Return: the flag that indicates whether the Entry is present or not
		 */
		public static boolean containsEntry(String id) {
			boolean entryFound = false; // flag, raised if an Entry having the same name as id is found
			Iterator<HashMap<String, SymTableEntry>> iterator = symTableStack.listIterator(0);
			while (entryFound || iterator.hasNext()) {
				if (iterator.next().containsKey(id)) entryFound = true;
			}
			return entryFound;
		}
		
		/*
		 * Check whether a certain Entry is present in the whole SymTable Stack and return it
		 * Search is applied from the top of the Stack to the bottom;
		 * the first match is considered
		 * Params: String id - the name of the Entry (the corresponding token)
		 * Return: the Entry itself, if found (null otherwise)
		 */
		public static SymTableEntry getEntry(String id) {
			boolean entryFound = false; // flag, raised if an Entry having the same name as id is found
			Iterator<HashMap<String, SymTableEntry>> iterator = symTableStack.listIterator(0);
			while (iterator.hasNext()) {
				HashMap<String, SymTableEntry> currentSymTable = iterator.next();
				if (currentSymTable.containsKey(id)) return currentSymTable.get(id);
			}
			return null;
		}
		
		/*
		 * Check whether a certain Entry is present in the whole SymTable Stack and return its TypeTree
		 * Search is applied from the top of the Stack to the bottom;
		 * the first match is considered
		 * Params: String id - the name of the Entry (the corresponding token)
		 * Return: the TypeTree of the Entry itself, if found (null otherwise)
		 */
		public static TypeTree getEntryTypeTree(String id) {
			SymTableEntry entry = SymTableStack.getEntry(id);
			if (debug_mode) System.err.println("WARNING: Inside getEntryTypeTree - no Entry found for " + id);
			return entry == null? null : entry.getTypeTree();
		}
		
		/*
		 * Check whether a certain Entry is present in the whole SymTable Stack and check if it has been already assigned
		 * Search is applied from the top of the Stack to the bottom;
		 * the first match is considered
		 * Params: String id - the name of the Entry (the corresponding token)
		 * Return: a Boolean flag indicating whether the variable has been assigned, null if the Entry has not been found
		 */
		public static Boolean getEntryAssignFlag(String id) {
			SymTableEntry entry = SymTableStack.getEntry(id);
			if (debug_mode) System.err.println("WARNING: Inside getEntryAssignFlag - no Entry found for " + id);
			return entry == null? null : entry.getIsAssigned();
		}
		
		/*
		 * Debugging method that prints out the content of the whole symTableStack 
		 * Params: nothing
		 * Returns: nothing
		 */
		public void dumpSymTableStack(){
			int level = 0;
			Iterator<HashMap<String, SymTableEntry>> iterator = symTableStack.listIterator(0);
			System.out.println("DEBUG: PRINTING SYMTABLESTACK!");
			while (iterator.hasNext()){
				System.out.println("Level " + level);
				iterator.next().forEach
					( (k, v) -> { 
						System.out.print("->");
						System.out.print(" Name: " + k);
						System.out.println(", isAssigned: " + v.getIsAssigned());
						v.getTypeTree().dumpTypeTree(1);
					});
				level++;
			}
		}
	}
:};

/*  In the Initialization Section, the typeMap (i.e. the list containing all declared types) is filled 
 *  with all the main Basic and Compound Types. 
 *  The only exception are Tuples, whose Type corresponds to the combination of the types of its elements;
 *  since such a Type would need too much space inside the typeMap, it is the ONLY Type that is not saved in the typeMap;
 *  so, it must be handled with specific procedures
 *  The "Any" Type acts as a wildcard and represents any possible Type (it represents Type Parameters)
 */
init with {:
		
		// childTypeTree is used only for making the code slightly more readable
		CUP$Parser$actions.TypeTree tree = new CUP$Parser$actions.TypeTree("Int", new CUP$Parser$actions.TypeTree[0]);
		CUP$Parser$actions.TypeTree.addType(tree.getTypeName(), tree);
		tree = new CUP$Parser$actions.TypeTree("Double", new CUP$Parser$actions.TypeTree[0]);
		CUP$Parser$actions.TypeTree.addType(tree.getTypeName(), tree);
		tree = new CUP$Parser$actions.TypeTree("Bool", new CUP$Parser$actions.TypeTree[0]);
		CUP$Parser$actions.TypeTree.addType(tree.getTypeName(), tree);
		tree = new CUP$Parser$actions.TypeTree("Char", new CUP$Parser$actions.TypeTree[0]);
		CUP$Parser$actions.TypeTree.addType(tree.getTypeName(), tree);
		CUP$Parser$actions.TypeTree[] treeArray = { new CUP$Parser$actions.TypeTree(tree) };
		tree = new CUP$Parser$actions.TypeTree("String", treeArray);
		CUP$Parser$actions.TypeTree.addType(tree.getTypeName(), tree);
		CUP$Parser$actions.TypeTree anyTypeTree = new CUP$Parser$actions.TypeTree("Any", new CUP$Parser$actions.TypeTree[0]); 
		treeArray = new CUP$Parser$actions.TypeTree[]{ new CUP$Parser$actions.TypeTree(anyTypeTree) };
		tree = new CUP$Parser$actions.TypeTree("List", treeArray);
		CUP$Parser$actions.TypeTree.addType(tree.getTypeName(), tree);
		treeArray = new CUP$Parser$actions.TypeTree[]{ new CUP$Parser$actions.TypeTree(anyTypeTree), new CUP$Parser$actions.TypeTree(anyTypeTree) };
		tree = new CUP$Parser$actions.TypeTree("Function", treeArray);
		CUP$Parser$actions.TypeTree.addType(tree.getTypeName(), tree);
		// Min Arity for Tuples = 2, Max Arity for Tuples = 16
		for (int i = 2; i <= 16; i++) {
			 treeArray = new CUP$Parser$actions.TypeTree[i];
			 for (int j = 0; j < i; j++) {
				treeArray[j] = new CUP$Parser$actions.TypeTree(anyTypeTree);
			 }
			 tree = new CUP$Parser$actions.TypeTree("Tuple" + i, treeArray); 
			 CUP$Parser$actions.TypeTree.addType(tree.getTypeName(), tree);
		}
:}

/* Because of a bug in JavaCupMainDrawTree, the "scan with" directive does not substitute the default call to next_token as expected.
 * Instead, the user code is just inserted before the aforementioned call, preventing the user from customizing 
 * the scanning method (an "Unreachable statement" compiler error arises). 
 * For this reason, it is necessary to manually force the user code execution
 * This is the reason behind the fake conditional statement below
 */
scan with {:   
		if (true) {
			Scanner scanner = (Scanner) this.getScanner();
			Symbol s = scanner.next_token_custom(); 
			Tree.push(s.toString(), 0); // necessary to build the graphical Parsing Tree
			return s;
		}	
:}

/// ///////////////////////// ///
///	  TERMINALS (lowercase)   ///
/// ///////////////////////// ///

terminal main;
terminal eq, clns, cm, ro, rc, bo, bc, arrow, sep/*, us */;
terminal plus, minus, times, div, mod, exp, uminus, and, or, not;
terminal releq, relge, relgt, relle, rellt;
terminal conc, cons;
terminal indent, dedent;
//terminal where, pipe;
terminal do_begin, if_begin, then, else_begin, let, in, print;
terminal val_int, val_double, val_bool, val_char, val_string;
terminal type_int, type_double, type_bool, type_char, type_string;
terminal id;

/// ///////////////////////// ///
///	NON TERMINALS (uppercase) ///
/// ///////////////////////// ///

non terminal PROGRAM;
non terminal FUNCT_PART, IMPER_PART;
non terminal IO_ACTIONS, IO_ACTION;
non terminal PRINT;
non terminal COND, EXPR, EXPR_NOTUNARYOP;
non terminal LEXPR, TEXPR;
non terminal ACTARG, LACTARG;
//non terminal WHERE, WHERE_STMTS, GUARDS, GUARD;
non terminal LET_STMTS;
non terminal DECL, DECL_TYPE, DECL_VALUE, DECL_FUNCT;
non terminal TYPE;
non terminal TYPE_VALUE, TYPE_FUNC;
non terminal TYPE_TUPLE, TYPE_LIST, TYPE_BASIC;
non terminal VALUE;
non terminal VALUE_LIST, VALUE_TUPLE, VALUE_BASIC;
non terminal TTYPE_VALUE;
//non terminal PATTERN, LPATTERN_CM, LPATTERN_CONS;
//non terminal PATTERN_LIST, PATTERN_TUPLE;
non terminal LFORMARG;

/// ///////////////////////// ///
///    PRECEDENCE RULES		  ///
/// ///////////////////////// ///

precedence left conc;
precedence right cons;
precedence left or;
precedence left and;
precedence nonassoc releq, relgt, relge, rellt, relle;
precedence left plus, minus;
precedence left times, div;
precedence right exp;
precedence nonassoc not, uminus;

start with PROGRAM;

/// ///////////////////////// ///
///	   PRODUCTION RULES       ///
/// ///////////////////////// ///
 
PROGRAM ::= /* empty program */ 
			{:
				if (noCompileErrors) {
				System.out.println("CODE COMPILED SUCCESSFULLY");
				}
			:}
		  | {:	// push the top-level symtable
				SymTableStack.pushSymTable();
				if (debug_mode) System.out.println("DEBUG: Top-level Symtable pushed");
			:}
			indent FUNCT_PART IMPER_PART {:
					SymTableStack.popSymTable();
					if (debug_mode) System.out.println("DEBUG: Top-level Symtable popped");
				:}
			dedent {:  // pop the top-level symtable
					if (noCompileErrors) {
						System.out.println("CODE COMPILED SUCCESSFULLY");
					}	
				:}
;

/// ////////////////// ///
///  IMPERATIVE PART   ///
/// ////////////////// ///

IMPER_PART ::= main eq indent IO_ACTION dedent
;

IO_ACTION ::= PRINT
            | do_begin indent IO_ACTIONS dedent
			| let {:
					SymTableStack.pushSymTable();
					if (debug_mode) System.out.println("DEBUG: Let Statement - Symtable pushed");
				:}
			  indent LET_STMTS dedent IO_ACTION {:
					SymTableStack.popSymTable();
					if (debug_mode) System.out.println("DEBUG: Let Statement - Symtable popped");
				:}
			| if_begin COND then IO_ACTION else_begin IO_ACTION
;

IO_ACTIONS ::= IO_ACTION
			 | IO_ACTIONS sep IO_ACTION
;

PRINT ::= print ACTARG
;

// DECL_FUNCT not supported (for now)
LET_STMTS ::= LET_STMTS sep DECL_TYPE
			| LET_STMTS sep DECL_VALUE
			| DECL_TYPE
			| DECL_VALUE
;

/// ////////////////// ///
///  FUNCTIONAL PART   ///
/// ////////////////// ///
 
FUNCT_PART ::= /* empty Functional section */
			 | FUNCT_PART DECL sep
;

/* special expression management for boolean conditions */
COND ::= EXPR 
;

///  DECLARATIONS  ///

DECL ::= DECL_TYPE
	   | DECL_VALUE
	   | DECL_FUNCT;

DECL_TYPE ::= id cm DECL_TYPE
			| id clns TYPE
; 

/* pattern and expr should be of the same type */
//DECL_VALUE ::= PATTERN eq EXPR
DECL_VALUE ::= id eq EXPR
;

/* no nullary functions */
DECL_FUNCT ::= id {: 
					SymTableStack.pushSymTable();
					if (debug_mode) System.out.println("DEBUG: Function Declaration - Symtable pushed");
			   :}
			   LFORMARG eq 
			   EXPR {:
					SymTableStack.popSymTable();
					if (debug_mode) System.out.println("DEBUG: Function Declaration - Symtable popped");
			   :}  
			    //WHERE
;

/* GUARDS ::= GUARDS sep GUARD
         | GUARD
;

   GUARD ::= pipe COND eq EXPR
; */

/* WHERE ::= // no where section 
        | where indent WHERE_STMTS dedent
; 

   WHERE_STMTS ::= WHERE_STMTS sep DECL 
			  | DECL
;*/

///  PATTERN MATCHING   ///

/*LFORMARG ::= LFORMARG PATTERN
		   | PATTERN
;

PATTERN ::= PATTERN_LIST
		  | PATTERN_TUPLE
		  | VALUE_BASIC
		  | us
		  | id
;

PATTERN_LIST ::= ro PATTERN cons LPATTERN_CONS rc // value constructor pattern
			   | bo LPATTERN_CM bc	// value pattern
			   | bo bc
;

LPATTERN_CONS ::= PATTERN
				| PATTERN cons LPATTERN_CONS
;

PATTERN_TUPLE ::= ro PATTERN cm LPATTERN_CM rc
				| ro rc
;

LPATTERN_CM ::= PATTERN
			  | PATTERN cm LPATTERN_CM
; */

LFORMARG ::= LFORMARG id
		   | id
;

///   EXPRESSIONS   ///

EXPR ::= EXPR plus EXPR_NOTUNARYOP
	   | EXPR minus EXPR_NOTUNARYOP
	   | EXPR times EXPR_NOTUNARYOP
	   | EXPR div EXPR_NOTUNARYOP
	   | mod ACTARG ACTARG 
	   | EXPR exp EXPR_NOTUNARYOP
	   | EXPR and EXPR_NOTUNARYOP
	   | EXPR or EXPR_NOTUNARYOP
	   | EXPR releq EXPR_NOTUNARYOP
	   | EXPR relgt EXPR_NOTUNARYOP
	   | EXPR relge EXPR_NOTUNARYOP
	   | EXPR rellt EXPR_NOTUNARYOP
	   | EXPR relle EXPR_NOTUNARYOP
	   | EXPR cons EXPR_NOTUNARYOP
	   | EXPR conc EXPR_NOTUNARYOP
	   | ro EXPR rc
	   | not EXPR   
	   | minus EXPR 		%prec uminus 
	   | let	{:
			SymTableStack.pushSymTable();
			if (debug_mode) System.out.println("DEBUG: Let Statement - Symtable pushed");
		 :} 
		 indent LET_STMTS dedent in EXPR {:
			SymTableStack.popSymTable();
			if (debug_mode) System.out.println("DEBUG: Let Statement - Symtable popped");
		 :}
	   | if_begin COND then EXPR else_begin EXPR
	   | id LACTARG 
	   | VALUE
;

EXPR_NOTUNARYOP ::= 
		 EXPR_NOTUNARYOP plus EXPR_NOTUNARYOP
	   | EXPR_NOTUNARYOP minus EXPR_NOTUNARYOP
	   | EXPR_NOTUNARYOP times EXPR_NOTUNARYOP
	   | EXPR_NOTUNARYOP div EXPR_NOTUNARYOP
	   | mod ACTARG ACTARG 
	   | EXPR_NOTUNARYOP exp EXPR_NOTUNARYOP
	   | EXPR_NOTUNARYOP and EXPR_NOTUNARYOP
	   | EXPR_NOTUNARYOP or EXPR_NOTUNARYOP
	   | EXPR_NOTUNARYOP releq EXPR_NOTUNARYOP
	   | EXPR_NOTUNARYOP relgt EXPR_NOTUNARYOP
	   | EXPR_NOTUNARYOP relge EXPR_NOTUNARYOP
	   | EXPR_NOTUNARYOP rellt EXPR_NOTUNARYOP
	   | EXPR_NOTUNARYOP relle EXPR_NOTUNARYOP
	   | EXPR_NOTUNARYOP cons EXPR_NOTUNARYOP
	   | EXPR_NOTUNARYOP conc EXPR_NOTUNARYOP
	   | ro EXPR rc
	   | let {:
			SymTableStack.pushSymTable();
			if (debug_mode) System.out.println("DEBUG: Let Statement - Symtable pushed");
		 :} 
		 indent LET_STMTS dedent in EXPR {:
			SymTableStack.popSymTable();
			if (debug_mode) System.out.println("DEBUG: Let Statement - Symtable popped");
		 :}
	   | if_begin COND then EXPR else_begin EXPR
	   | id LACTARG 
	   | VALUE
;

///    args for function call   ///

ACTARG ::= id 
         | VALUE
		 | ro EXPR rc
;

// list of input arguments for function call
LACTARG ::= /* empty list of args: we call a variable */
		  | LACTARG ACTARG
;

///   VALUES   ///

VALUE ::= VALUE_BASIC
		| VALUE_LIST
		| VALUE_TUPLE
;

VALUE_BASIC ::= val_int
			  | val_double
			  | val_bool
			  | val_char
			  | val_string
;

VALUE_LIST ::= bo LEXPR bc
			 | bo bc
;

VALUE_TUPLE ::= ro TEXPR rc 
			  | ro rc
;

LEXPR ::= EXPR
		| LEXPR cm EXPR
;

TEXPR ::= EXPR cm EXPR
		| TEXPR cm EXPR
;
///   TYPES   ///

TYPE ::= TYPE_VALUE
	   | TYPE_FUNC
;

// both basic types and compound
TYPE_VALUE ::= TYPE_TUPLE
			 | TYPE_LIST
			 | TYPE_BASIC
;

TYPE_BASIC ::= type_int
			 | type_double
			 | type_bool
			 | type_char
			 | type_string
;

TYPE_LIST ::= bo TYPE_VALUE bc
;

TYPE_TUPLE ::= ro rc 
			 | ro TTYPE_VALUE rc
;

// no higher order functions
TYPE_FUNC ::= TYPE_VALUE arrow TYPE_FUNC
		    | TYPE_VALUE arrow TYPE_VALUE
;

TTYPE_VALUE ::= TYPE_VALUE cm TYPE_VALUE
			  | TTYPE_VALUE cm TYPE_VALUE
;



